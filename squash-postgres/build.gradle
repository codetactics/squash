repositories {
    maven { url 'https://dl.bintray.com/e5l/ktor' }
    maven { url 'https://dl.bintray.com/kotlin/kotlinx' }
}

sourceSets {
    jdbc
    cio
}

configurations {
    jdbcClasses {
        extendsFrom(jdbcRuntime)
        
    }
    cioClasses {
        extendsFrom(cioRuntime)
    }
}

task jdbcJar(type: Jar) {
    classifier = 'jdbc'
    from sourceSets.jdbc.output
}

task cioJar(type: Jar) {
    classifier = 'cio'
    from sourceSets.cio.output
}

artifacts {
    jdbcClasses jdbcJar
    cioClasses cioJar
}

dependencies {
    compile project(':squash-core')
    compile project(':squash-graph')
    
    jdbcCompile project(':squash-jdbc')
    jdbcCompile 'org.postgresql:postgresql:42.2.5'
    jdbcCompile sourceSets.main.output

    cioCompile project(':squash-core')
    cioCompile project(':squash-cio')
    cioCompile 'io.ktor.clients:ktor-client-postgre:0.0.1'
    cioCompile sourceSets.main.output
    
    testCompile 'org.apache.commons:commons-io:1.3.2'
    testCompile 'de.flapdoodle.embed:de.flapdoodle.embed.process:2.0.5'
    testCompile 'ru.yandex.qatools.embed:postgresql-embedded:2.9'

    // Depends on jdbc source set and its runtime
    testCompile sourceSets.jdbc.output
    testCompile configurations.jdbcRuntime

    // Depends on jdbc source set and its runtime
    testCompile sourceSets.cio.output
    testCompile configurations.cioRuntime
    
    testCompile project(':squash-core')
    testCompile project(':squash-graph')
    testCompile project(path: ':squash-core', configuration: 'testClasses')
    testCompile project(path: ':squash-graph', configuration: 'testClasses')
}

compileJdbcKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
